#!/usr/bin/env perl6

use v6.d.PREVIEW;

use Blin::Module;
use Blin::Processing;

use Whateverable;
use Whateverable::Builds;
use Whateverable::Config;
use Whateverable::Running;

unit sub MAIN(
    #| Old revision (initialized to the last release if unset)
    Str :old($start-point) is copy,
    #| New revision (default: HEAD)
    Str :new($end-point) = â€˜HEADâ€™,
    #| Number of threads to use (initialized to the output of `nproc` if unset)
    Int :$nproc is copy,
    #| Thread number multiplier (default: 1.0)
    Rat :$nproc-multiplier = 1.0,
    #| Number of extra runs for regressed modules (default: 4)
    Int :$deflap = 4, # Can be really high because generally we are
                      # not expecting a large fallout with many
                      # now-failing modules.
    #| Number of seconds between printing the current status (default: 60.0)
    Rat :$heartbeat = 60.0,
    #| Use this to test some specific modules (empty = whole ecosystem)
    *@specified-modules,
);

#| Where to pull source info from
my @sources       = <
    https://raw.githubusercontent.com/ugexe/Perl6-ecosystems/master/cpan.json
    http://ecosystem-api.p6c.org/projects.json
>; # TODO steal that from zef automatically

#| Core modules that are ignored as dependencies
my $ignored-deps  = <Test NativeCall Pod::To::Text Telemetry snapper>.Set;

#| Modules that should not be installed at all
my $havoc-modules = âˆ…;

#| Modules with tests that we don't want to run
my $skip-tests = (
   â€˜MoarVM::Remoteâ€™, # possibly harmless, but scary anyway
   â€˜Novemberâ€™, # eats memory
   # These seem to hang and leave some processes behind:
   â€˜IO::Socket::Async::SSLâ€™,
   â€˜IRC::Clientâ€™,
   # These were ignored by Toaster, but reasons are unknown:
   â€˜HTTP::Server::Asyncâ€™,
   â€˜HTTP::Server::Threadedâ€™,
   â€˜Log::Minimalâ€™,
   â€˜MeCabâ€™,
   â€˜Time::Durationâ€™,
   â€˜Toasterâ€™,
   â€˜Uzuâ€™,
).Set;


#| Where to install zef
my $zef-path      = â€˜data/zefâ€™.IO;
#| Some kind of a timeout ğŸ˜‚
my $timeout       = 60 Ã— 10;

my $semaphore;

my $output-path   = â€˜outputâ€™.IO;
my $overview-path = $output-path.add: â€˜overviewâ€™;
my $dot-path      = $output-path.add: â€˜overview.dotâ€™;
my $svg-path      = $output-path.add: â€˜overview.svgâ€™;

mkdir $output-path;
unlink $overview-path;
unlink $dot-path;
unlink $svg-path;

# Initialized later
my $start-point-full;
my   $end-point-full;

my $save-lock = Lock.new; # to eliminate miniscule chance of racing when saving


#âœ-----------------------------cut-here------------------------------


# Hey reader, are you expecting some fancy pancy algorithms here?
# Well, let me disappoint you! Hundreds of `whenever`s are set up to
# start testing modules once their dependencies are processed. There's
# a little bit of depth-first search to find required dependencies
# (with marking of visited modules to spot cycles), but otherwise the
# code is dumb as bricks. It should scale up to the amount of modules
# in CPAN, at least as long as Rakudo is able to keep itself together
# with thousands of whenevers. In any case, don't quote me on that. At
# CPAN scale you'd have other problems to deal with anyway.


note â€˜ğŸ¥ Prepâ€™;

$nproc //= ($nproc-multiplier Ã— +run(:out, â€˜nprocâ€™).out.slurp).Int;
$semaphore = Semaphore.new: $nproc.Int;

note â€œğŸ¥ Will use up to $nproc threads for testing modulesâ€;

ensure-config â€˜./config-default.jsonâ€™;
pull-cloned-repos; # pull rakudo and other stuff

$start-point //= get-tags(â€˜2015-12-24â€™, :default()).tail;

note â€œğŸ¥ Will compare between $start-point and $end-pointâ€;

if $zef-path.d {
    run :cwd($zef-path), <git pull>
} else {
    run <git clone https://github.com/ugexe/zef>, $zef-path
}

$start-point-full = to-full-commit $start-point;
  $end-point-full = to-full-commit   $end-point;

die â€˜Start point not foundâ€™ unless $start-point-full;
die   â€˜End point not foundâ€™ unless   $end-point-full;


my $quick-test = â€˜/tmp/quick-test.p6â€™;
spurt $quick-test, â€œsay 42\nâ€;

die â€˜No build for start pointâ€™ unless build-exists $start-point-full;
die â€˜No build for end pointâ€™   unless build-exists   $end-point-full;
die â€˜Dead start pointâ€™ if run-snippet($start-point-full, $quick-test)<output>.chomp ne 42;
die â€˜Dead end pointâ€™   if run-snippet(  $end-point-full, $quick-test)<output>.chomp ne 42;


# Leave some builds unpacked
my @always-unpacked = $start-point-full, $end-point-full;
run-smth $_, {;}, :!wipe for @always-unpacked;

note â€˜ğŸ¥  Modules and stuffâ€™;

my @modules;
my %lookup; # e.g. %(foo => [Module foo:v1, â€¦], â€¦)


note â€˜ğŸ¥ğŸ¥ Populating the module list and the lookup hashâ€™;
for @sources {
    use JSON::Fast;
    # XXX curl because it works
    my $json-data = run(:out, <curl -->, $_).out.slurp;
    my $json = from-json $json-data;
    for @$json {
        my Module $module .= new:
            name    => .<name>,
            version => Version.new(.<version>) // v0,
            depends => ([âˆª]
                        (.<      depends> // âˆ…).Set,
                        (.< test-depends> // âˆ…).Set,
                        (.<build-depends> // âˆ…).Set,
                       ) âˆ– $ignored-deps,
        ;
        if $module.name âˆˆ $havoc-modules {
            note â€œğŸ¥ğŸ¥ Module {$module.name} is ignored because it causes havocâ€;
            next
        }

        @modules.push: $module;
        %lookup{$module.name}.push: $module;
        %lookup{.key}.push: $module for .<provides>.pairs; # practically aliases
    }
}


note â€˜ğŸ¥ğŸ¥ Sorting modulesâ€™;
.value = .value.sort(*.version).eager for %lookup;


note â€˜ğŸ¥ğŸ¥ Resolving dependenciesâ€™;
for @modules -> $module {
    sub resolve-dep($depstr) {
        return Empty if $depstr !~~ Str; # weird stuff, only in Inline::Python
        use Zef::Distribution::DependencySpecification;
        my $depspec = Zef::Distribution::DependencySpecification.new: $depstr;
        if ($depspec.spec-parts<from> // â€˜â€™) eq <native bin>.any {
            # TODO do something with native deps?
            return Empty
        }
        my $depmodule = %lookup{$depspec.name // Empty}.tail;
        without $depmodule {
            $module.done.keep: MissingDependency if not $module.done;
            $module.errors.push: â€œDependency â€œ$depstrâ€ was not resolvedâ€;
            return Empty
        }
        $depmodule
    }
    $module.depends = $module.depends.keys.map(&resolve-dep).Set;
    .rdepends âˆª= $module for $module.depends.keys;
}


note â€˜ğŸ¥ğŸ¥ Marking latest versions and their depsâ€™;
for %lookup {
    next unless .key eq .valueÂ».name.any; # proceed only if not an alias
    next if @specified-modules and not .key eq @specified-modules.any;
    .value.tail.needify
}


note â€˜ğŸ¥ğŸ¥ Filtering out uninteresting modulesâ€™;
@modules .= grep: *.needed;


note â€˜ğŸ¥ğŸ¥ Detecting cyclic dependenciesâ€™;
for @modules -> $module {
    eager gather $module.safe-deps: True;
    CATCH {
        when X::AdHoc { # TODO proper exception
            $module.done.keep: CyclicDependency if not $module.done;
            $module.errors.push: â€˜Cyclic dependency detectedâ€™;
        }
    }
}


note â€˜ğŸ¥ğŸ¥ Listing some early errorsâ€™;
for @modules {
    next unless .done;
    put â€œ{.name} â€“ {.done.result} â€“ {.errors}â€;
}


note â€˜ğŸ¥ Processingâ€™;
my $processing-done = Promise.new;
start { # This is just to print something to the terminal regularly
    react {
        whenever Supply.interval: $heartbeat { # just something we print from time to time
            save-overview; # make sure we save something if it hangs
            my $total  = +@modules;
            my @undone = eager @modules.grep: *.done.not;
            my $str    = â€œâ³ {$total - @undone} out of $total modules processedâ€;
            $str      ~= â€˜ (left: â€™ ~ @undoneÂ».name ~ â€˜)â€™ if @undone â‰¤ 5;
            note $str;
            done unless @undone;
        }
        whenever $processing-done {
            done
        }
    }
    CATCH {
        default {
            note â€˜uh oh in heartbeatâ€™; note .gist;
        }
    }
}

react { # actual business here
    for @modules -> $module {
        next if $module.done;
        whenever Promise.allof($module.depends.keysÂ».done) {
            # Important: the `acquire` below has to be outside of the
            # start block. Otherwise we can cause thread starvation by
            # kicking off too many start blocks at the same time
            # (because there are many modules that don't depend on
            # anything). Basically, all of the `start` blocks will
            # cause Proc::Async's not to start, meaning that start
            # blocks will never finish either. Another (better) option
            # to resolve this (maybe) is to use another
            # ThreadPoolScheduler. Note that as of today machines with
            # a gazzilion of cores (where the number of cores gets
            # close to the default value of 64) will need to use
            # RAKUDO_MAX_THREADS env variable.
            $semaphore.acquire;
            start {
                LEAVE $semaphore.release;

                process-module $module,
                               :$deflap,
                               :$start-point-full, :$end-point-full,
                               :$zef-path, :$timeout,
                               :@always-unpacked,
                               testable => $module.name âˆ‰ $skip-tests,
                ;

                CATCH {
                    default {
                        note â€˜uh oh in processingâ€™; note .gist;
                        $module.done.keep: UnhandledException;
                    }
                }
            }
        }
    }
}

note â€˜ğŸ¥ğŸ¥ Almost done, waiting for all modules to finishâ€™;
await @modulesÂ».done;


$processing-done.keep;
note â€˜ğŸ¥ Saving resultsâ€™;

note â€˜ğŸ¥ğŸ¥ Saving the overviewâ€™;

sub save-overview {
    $save-lock.protect: {
        spurt $overview-path, @modules.sort(*.name).map({
            my $result = .done ?? .done.result !! Unknown;
            my $line = â€œ{.name} â€“ $resultâ€;
            $line ~= â€œ, Bisected: {.bisected}â€ if $result == Fail;
            $line
        }).join: â€œ\nâ€
    }
}

save-overview;

note â€˜ğŸ¥ğŸ¥ Saving the dot fileâ€™;
my @bisected = @modules.grep(*.done.result == Fail);
# Not algorithmicaly awesome, but will work just fine in practice
my Set $to-visualize = @bisected.Set;
$to-visualize âˆª= (gather  .deps: True).Set for @bisected;
$to-visualize âˆª= (gather .rdeps: True).Set for @bisected;

my $dot = â€˜â€™;

for $to-visualize.keys -> $module {
    my $color = do given $module.needed ?? $module.done.result !! Unknown {
        when Unknown                { â€˜grayâ€™        }
        when OK                     { â€˜greenâ€™       }
        when Fail                   { â€˜redâ€™         }
        when Flapper                { â€˜yellowâ€™      }
        when AlwaysFail             { â€˜violetâ€™      }
        when InstallableButUntested { â€˜yellowgreenâ€™ }
        when MissingDependency      { â€˜orangeâ€™      }
        when CyclicDependency       { â€˜blueâ€™        }
        when BisectFailure          { â€˜brownâ€™       }
        when ZefFailure             { â€˜crimsonâ€™     }
        when UnhandledException     { â€˜hotpinkâ€™     }
    }
    $dot ~= â€œ    "{$module.name}" [color=$color];\nâ€;
    for $module.depends.keys {
        next unless $_ âˆˆ $to-visualize;
        $dot ~= â€œ    "{$module.name}" -> "{.name}";\nâ€;
    }
    $dot ~= â€œ\nâ€;
}

if $dot {
    spurt $dot-path, â€œdigraph \{\n    rankdir = BT;\nâ€ ~ $dot ~ â€œ\n}â€;
    note â€˜ğŸ¥ğŸ¥ Creating an SVG image from the dot fileâ€™;
    run <dot -T svg -o>, $svg-path, $dot-path # TODO -- ?
} else {
    note â€˜ğŸ¥ğŸ¥ No regressions found, dot file not savedâ€™;
}

note â€˜ğŸ¥ Cleaning upâ€™;
for @always-unpacked {
    my $path = run-smth-build-path $_;
    run <rm -rf -->, $path; # TODO use File::Directory::Tree ?
}
